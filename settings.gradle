boolean travis = (System.env['TRAVIS'] ?: false) as boolean
boolean appveyor = (System.env['APPVEYOR'] ?: false) as boolean
boolean fullBuild = !travis && !appveyor

def includeProject = { String projectDirName, String projectName ->
    File baseDir = new File(settingsDir, projectDirName)
    File projectDir = new File(baseDir, projectName)
    String buildFileName = "${projectName}.gradle"

    assert projectDir.isDirectory()
    assert new File(projectDir, buildFileName).isFile()

    include projectName
    project(":${projectName}").projectDir    = projectDir
    project(":${projectName}").buildFileName = buildFileName
}

rootProject.name = 'basilisk'

includeProject 'subprojects', 'basilisk-core'
includeProject 'subprojects', 'basilisk-core-compile'
includeProject 'subprojects', 'basilisk-core-test'
includeProject 'subprojects', 'basilisk-guice'
includeProject 'subprojects', 'basilisk-javafx'
includeProject 'subprojects', 'basilisk-javafx-test'
includeProject 'subprojects', 'gradle-basilisk-plugin'
includeProject 'subprojects', 'gradle-basilisk-build-plugin'

if (!appveyor) {
    includeProject 'samples', 'sample-javafx-java'
}

if (fullBuild) {
    includeProject 'templates', 'basilisk-application-templates'
    includeProject 'templates', 'basilisk-standard-templates'
}
