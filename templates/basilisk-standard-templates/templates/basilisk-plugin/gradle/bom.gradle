apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

generateBom {
    additionalDependencies = []
}

publishing {
    publications {
        mavenBom(MavenPublication) {
            artifacts = []
        }
    }
}

publishToMavenLocal.dependsOn generateBom

gradle.taskGraph.whenReady { g ->
    def generatePom = gradle.taskGraph.allTasks.find { it.name == 'generatePomFileForMavenBomPublication' }
    def publicationExt = project.extensions.findByType(PublishingExtension)
    def pub = publicationExt.publications.findByName('mavenBom')
    generatePom?.doLast {
        copy {
            into pub.asNormalisedPublication().pomFile.parentFile
            from generateBom.outputFile
            rename generateBom.outputFile.name, 'pom-default.xml'
        }
    }
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    publications = ['mavenBom']
    pkg {
        repo = 'basilisk-plugins'
        userOrg = 'basilisk'
        name = project.name
        desc = project.projectDescription
        licenses = ['Apache-2.0']
        labels = project.projectLabels.split(',')
        websiteUrl = project.projectWebsiteUrl
        issueTrackerUrl = project.projectIssueTrackerUrl
        vcsUrl = project.projectVcsUrl
        publicDownloadNumbers = true
    }
}